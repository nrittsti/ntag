plugins {
    id 'java'
    id 'java-library-distribution'
}

group = 'com.github'
version = '1.2.3'
sourceCompatibility = '13'
targetCompatibility = '13'
jar.archiveFileName = 'ntag.jar'
distTar.archiveFileName = "ntag-${platform}_bin.tar.gz"
distZip.archiveFileName = "ntag-${platform}_bin.zip"
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
}

dependencies {
    implementation files('libs/jaudiotagger-2.2.4.jar')
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.glassfish:javax.json:1.1.4'
    implementation "org.openjfx:javafx-base:${fxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${fxVersion}:${platform}"
    implementation "org.openjfx:javafx-fxml:${fxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${fxVersion}:${platform}"
    implementation "org.openjfx:javafx-swing:${fxVersion}:${platform}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterVersion}"
    testImplementation 'org.testfx:testfx-junit5:4.0.16-alpha'
    testImplementation 'org.assertj:assertj-core:3.14.0'
    testRuntimeOnly 'org.testfx:openjfx-monocle:jdk-12.0.1+2'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
}

test {
    systemProperty "glass.platform", "Monocle"
    systemProperty "monocle.platform", "Headless"
    systemProperty "testfx.robot", "glass"
    systemProperty "testfx.headless", "true"
    systemProperty "prism.order", "sw"
    systemProperty "prism.text", "t2k"
    systemProperty "java.awt.headless", "true"
    useJUnitPlatform {
        includeTags tests.split(",")
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

distributions {
    main {
        distributionBaseName = "ntag-${platform}_bin"
        contents {
            from 'doc'
            from 'etc'
        }
    }
}

jar {
    manifest {
        attributes("Implementation-Title": "NTag",
                "Implementation-Version": archiveVersion,
                "Built-By": System.properties['user.name'],
                "Build-Jdk": JavaVersion.current(),
                "Main-Class": 'ntag.NTag',
                "Class-Path": sourceSets.main.runtimeClasspath.findAll{ it.getName() != 'main' }.collect { 'lib/' + it.getName() }.join(' ')
        )
    }
}

distZip {
    eachFile { file ->
        String path = file.relativePath
        file.setPath("ntag-${archiveVersion.get()}/" + path.substring(path.indexOf("/")+1,path.length()))
    }
    includeEmptyDirs = false
}

distTar {
    eachFile { file ->
        String path = file.relativePath
        file.setPath("ntag-${archiveVersion.get()}/" + path.substring(path.indexOf("/")+1,path.length()))
    }
    includeEmptyDirs = false
    compression = Compression.GZIP
}

task run(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "ntag.NTag"
    jvmArgs = [
            '--module-path',
            classpath.asPath,
            '--add-modules',
            'javafx.controls,javafx.fxml,javafx.swing,java.logging,jaudiotagger,java.desktop,java.xml.bind'
    ]
}
